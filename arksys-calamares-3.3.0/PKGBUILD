# https://github.com/calamares/calamares/releases

pkgname=arksys-calamares
_pkgname=calamares
pkgver=3.3.0.r10619.4a821b6e3
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/calamares/calamares/releases"

depends=('appstream-qt' 'boost-libs' 'ckbcomp' 'efibootmgr' 'gtk-update-icon-cache' 'icu'
         'kconfig' 'kcoreaddons' 'kdbusaddons' 'kiconthemes' 'ki18n' 'kio' 'kpmcore' 'hwinfo'
         'libpwquality' 'mkinitcpio-openswap' 'plasma-framework' 'polkit-qt5' 'qt5-svg'
         'qt5-xmlpatterns' 'squashfs-tools' 'solid' 'yaml-cpp')
makedepends=('boost' 'extra-cmake-modules' 'git' 'qt5-tools' 'qt5-translations')

backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

# https://github.com/erikdubois/calamares o https://github.com/arksys-os/calamares
source=("$_pkgname::git+https://github.com/erikdubois/calamares"
	"calamares.desktop"
	"calamares_polkit")

sha256sums=('SKIP'
            '05c4ee3398ebc5168332888f83e49baf9e143641f11873eed14fcd2b6e409a54'
            'c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3')

# sha256 calamares.desktop > 05c4ee3398ebc5168332888f83e49baf9e143641f11873eed14fcd2b6e409a54
# sha256 calamares_polkit  > c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3

pkgver() {
	cd ${srcdir}/$_pkgname
	_ver="$(cat CMakeLists.txt | grep -m1 -e "set(CALAMARES_VERSION"  | xargs | sed s'/set(CALAMARES_VERSION.//g' | sed s'/)//g' | sed s'/-/./g')"
	_git=".r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
	printf "3.3.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	sed -i -e "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${_ver}${_git}|g" CMakeLists.txt
	sed -i -e "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt
}

prepare() {
	cp -rv ../modules/* ${srcdir}/$_pkgname/src/modules/

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/${_pkgname}/CMakeLists.txt"
	sed -i -e 's/# DEBUG_FILESYSTEMS/DEBUG_FILESYSTEMS/' "$srcdir/${_pkgname}/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "$srcdir/${_pkgname}/src/modules/fstab/main.py"

	# add pkgrelease to patch-version
	cd ${_pkgname}
	_patchver="$(cat CMakeLists.txt | grep -m3 -e CALAMARES_VERSION_PATCH | grep -o "[[:digit:]]*" | xargs)"
	sed -i -e "s|CALAMARES_VERSION_PATCH $_patchver|CALAMARES_VERSION_PATCH $_patchver-$pkgrel|g" CMakeLists.txt
}

build() {
	cd $_pkgname

	mkdir -p build
	cd build
	cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PYTHONQT=OFF \
	-DWITH_KF5DBus=OFF \
	-DBoost_NO_BOOST_CMAKE=ON \
	-DWEBVIEW_FORCE_WEBKIT=OFF \
	-DSKIP_MODULES="webview tracking interactiveterminal initramfs \
	initramfscfg dracut dracutlukscfg \
	dummyprocess dummypython dummycpp \
	dummypythonqt services-openrc \
	keyboardq localeq welcomeq"
	make
}

package() {
	cd ${_pkgname}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
}

# Based on https://github.com/arcolinux/arcolinux-pkgbuild-calamares/blob/master/arco-calamares-3.3.0-alpha6-01/PKGBUILD

pkgname=arksys-calamares
_pkgname=calamares
pkgver=3.3.0.alpha6
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=('GPL')

url="https://github.com/calamares/calamares/releases"
depends=('appstream-qt' 'boost-libs' 'ckbcomp' 'efibootmgr' 'gtk-update-icon-cache' 'icu'
         'kconfig' 'kcoreaddons' 'kdbusaddons' 'kiconthemes' 'ki18n' 'kio' 'kpmcore' 'hwinfo'
         'libpwquality' 'mkinitcpio-openswap' 'plasma-framework' 'polkit-qt5' 'qt5-svg'
         'qt5-xmlpatterns' 'squashfs-tools' 'solid' 'yaml-cpp')
makedepends=('boost' 'extra-cmake-modules' 'git' 'qt5-tools' 'qt5-translations')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

# Fork in https://github.com/arksys-os/calamares
source=("calamares::https://github.com/erikdubois/calamares/archive/refs/tags/$pkgver.tar.gz"
	    "calamares.desktop"
		"calamares-debugging.desktop"
	    "calamares_polkit")

sha256sums=('5b8792d0e91ee7ae889d4e3f1f67b3626279b677527b0211b4304d786f9847a4'
            '0d7a144638096e9b4808b83a0abd61b0ebac49e97c9ac0a43200b42e1ea767de'
            '873746c38de84d1c54f15255a993ac51f3d2a0958b6acb143f1e6b2300ebfa61'
            'c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3')

#pkgver() {
	# cd ${srcdir}/$_pkgname-${pkgver}
	# _ver="$(cat CMakeLists.txt | grep -m3 -e "  VERSION" | grep -o "[[:digit:]]*" | xargs | sed s'/ /./g')"
	# _git=".r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
	# printf '%s%s' "${_ver}" #"${_git}"
#}

prepare() {
	cp -rv ../modules/* ${srcdir}/$_pkgname-${pkgver}/src/modules/

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/${_pkgname}-${pkgver}/CMakeLists.txt"
	sed -i -e 's/# DEBUG_FILESYSTEMS/DEBUG_FILESYSTEMS/' "$srcdir/${_pkgname}-${pkgver}/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "$srcdir/${_pkgname}-${pkgver}/src/modules/fstab/main.py"

	# add pkgrelease to patch-version
	cd ${_pkgname}-${pkgver}
	_patchver="$(cat CMakeLists.txt | grep -m3 -e CALAMARES_VERSION_PATCH | grep -o "[[:digit:]]*" | xargs)"
	#sed -i -e "s|CALAMARES_VERSION_PATCH $_patchver|CALAMARES_VERSION_PATCH $_patchver-$pkgrel|g" CMakeLists.txt
	sed -i -e "s|CALAMARES_VERSION 3.3.0-alpha3|CALAMARES_VERSION $_pkgver-$_alpha-$pkgrel|g" CMakeLists.txt
}

build() {
	cd $_pkgname-$pkgver

	mkdir -p build
	cd build
	cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PYTHONQT=OFF \
	-DWITH_KF5DBus=OFF \
	-DBoost_NO_BOOST_CMAKE=ON \
	-DWEBVIEW_FORCE_WEBKIT=OFF \
	-DSKIP_MODULES="webview tracking interactiveterminal initramfs \
	initramfscfg dracut dracutlukscfg \
	dummyprocess dummypython dummycpp \
	dummypythonqt services-openrc \
	keyboardq localeq welcomeq"
	make
}

package() {
	cd ${_pkgname}-${pkgver}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm644 "$srcdir/calamares-debugging.desktop" "$pkgdir/usr/share/applications/calamares-debugging.desktop"
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	#rm "$pkgdir/usr/share/applications/calamares.desktop"
}
